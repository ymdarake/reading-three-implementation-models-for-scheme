# 3.4 Implementing the Heap-Based Model

## 3.4.1 Assembly Code
### The assembly code for the VM described in this section consists of **12 instructions** each with zero or more operands. The instructions are described below.
- **(halt)**
    - halts the virtual machine.
    - The value in the accumulator is the result of the computation.
- **(refer var x)**
    - finds the value of the variable var in the current environment
    - places this value into the accumulator
    - sets the next expression to x.
- **(constant obj x)**
    - places obj into the the accumulator and sets the next expression to x.
- **(close vars body x )**
    - creates a closure from body, vars and the current environment
    - places the closure into the accumulator
    - sets the next expression to x.
- **(test then else)**
    - tests the accumulator and if the accumulator is nonnull (that is, the test returned true)
    - sets the next expression to then
    - Otherwise test sets the next expression to else.
- **(assign var x)**
    - changes the current environment binding for the variable var to the value in the accumulator
    - sets the next expression to x.
- **(conti x)**
    - creates a continuation from the current stack, places this continuation in the accumulator
    - sets the next expression to x.
- **(nuate s var)**
    - restores s to be the current stack, sets the accumulator to the value of var in the current environment
    - sets the next expression to **(return)** (see below).
- **(frame x ___ret___ )**
    - creates a new frame from the current environment, the current rib, and ___ret___ as the next expression
    - adds this frame to the current stack
    - sets the current rib to the empty list
    - sets the next expression to x.
- **(argument x)**
    - adds the value in the accumulator to the current rib
    - sets the next expression to x.
- **(apply)**
    - applies the closure in the accumulator to the list of values in the current rib.
    - Precisely this instruction
        - extends the closure's environment with the closure's variable list and the current rib
        - sets the current environment to this new environment
        - sets the current rib to the empty list
        - sets the next expression to the closure's body.
- **(return)**
    - removes the first frame from the stack
    - resets the current environment, the current rib, the next expression, and the current stack.

## 3.4.2 Translation
### The compiler transforms Scheme expressions into the assem- bly language instructions listed above.

Some Scheme expressions, such as variables and constants, are transformed into a single assembly language instruction. Oth- ers, such as applications, are turned into several instructions.

### The inputs to the compiler are the expression to compile and the next instruction to perform after the expression completes.
```scheme
(define compile
  (lambda (x next)
    (cond
      [(symbol? x)
       (list 'refer x next)]
      [(pair? x)
       (record-case x
                    [quote (obj)
                           (list 'constant obj next)]
                    [lambda (vars body)
                      (list 'close vars (compile body '(return)) next)]
                    [if (test then else)
                        (let ([thenc (compile then next)]
                              [elsec (compile else next)])
                          (compile test (list 'test thenc elsec)))]
                    [set! (var x)
                          (compile x (list 'assign var next))]
                    [call/cc (x); x => lambda
                             (let ([c (list 'conti
                                            (list 'argument
                                                  (compile x '(apply))))])
                               (if (tail? next)
                                   c
                                   (list 'frame next c)))]
                    [else
                     (recur loop ([args (cdr x)]
                                  [c (compile (car x) '(apply))]); this line is for the Final step
                       (if (null? args)
                           (if (tail? next)
                               c; (*cf) Final step of the apply instruction
                               (list 'frame next c)); (*cf) Final step of the apply instruction
                           (loop (cdr args)
                                 (compile (car args)
                                          (list 'argument c)))))])]
      [else
       (list 'constant x next)])))
```

### Both if and set! need one of their subexpressions to be evaluated before the real work of the expression can be done. This is where the next argument to the compiler becomes useful.
- For an if expression of the form (if test then else)
    - the test subexpression is compiled with a next argument formed from the compiled then and else subexpressions, (test cthen celse).
    - This result of compiling the test subexpression with this next argument is returned as the compiled form of the if expression.
    - **Incidentally, by passing on the original next argument when compiling both of the then and else subexpressions, the compiler is creating a graph structure**.
        - This is how the use of labels and jumps is avoided.
- A set! expression is treated in a manner similar to an if expression.
    - (set! var x) is transformed into the compiled representation of x with a next instruction of (assign var next), where next is the original argument to compile.
- An application of the form (fcn arg1 ... argn) is transformed into an instruction “sequence” of the form:
```
frame
    argn
        argument
            ...
                arg1
                    argument
                        fcn
                            apply
```
- The first instruction to be performed will be a frame instruction of the form **(frame c next)**, where c refers to the compiled code to perform the application and next is the next instruction argument to the compiler (**this is the return address of the application**).
- The true next instruction, c, is the compiled code for the last argument, whose next instruction is the argument instruction.
    - Its next instruction is the compiled code for the second to last argument, followed by another argument instruction, and so on, through the first argument and the corresponding argument instruction.
- (*cf) **Finally, the next instruction after the last argument instruction is the apply instruction**.
- **The arguments to an application are evaluated last to first so that they will be "consed" onto the value rib in the right order**. cons adds elements to the front of a list, so the last object pushed is the first object on the list.

### A call/cc may be thought of as a special case of an application with a single argument, an imaginary expression that returns the current continuation.
- An expression of the form (call/cc exp) results in an instruction sequence of the form:
```
frame
    conti
        argument
            exp
                apply
```
- This causes
    - the frame to be pushed
    - followed by creation of the continuation
    - the adding of this continuation to the current rib
    - the computation of exp
    - the application of exp to the list of arguments containing the continuation.

### Both applications and call/cc expressions are treated slightly differently if they appear in the tail position. This is determined simply by looking at the next instruction to see if it is a return instruction as follows:
```scheme
(define tail?
    (lambda (next)
        (eq? (car next) 'return)))
```
An application or call/cc expression in tail position does not push a call frame,
**so the frame instruction is omitted**.